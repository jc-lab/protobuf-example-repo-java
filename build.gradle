buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.16.1"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.14"
    }
}

def getProtoVersion = { ->
    def text = ''
    def fis = file("../../version").withReader {
        reader -> text = reader.readLines().stream()
                .map { it.trim() }
                .filter { !it.empty }
                .collect(java.util.stream.Collectors.joining(""))
    }
    return Integer.parseInt(text)
}

def getGitRevision = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

apply plugin: 'java'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'com.google.protobuf'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

group 'com.example'
version "${System.getenv('BUILD_MAJOR_VERSION')}.${getGitRevision}.${System.getenv('BUILD_REVISION')}"

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.34.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc { }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/protobuf'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
    test {
        proto {
            // In addition to the default 'src/test/proto'
            srcDir 'src/test/protobuf'
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.google.protobuf:protobuf-java:3.14.0'
    compile 'io.grpc:grpc-stub:1.34.1'
    compile 'io.grpc:grpc-protobuf:1.34.1'
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }
}

publishing {
// ...
}
